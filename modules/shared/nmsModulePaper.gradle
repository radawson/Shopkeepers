apply plugin: 'java-library'
apply plugin: 'org.checkerframework'
apply plugin: 'eclipse'
apply plugin: 'io.papermc.paperweight.userdev'

// Every project using this script must declare this version BEFORE applying this script:
/*ext {
	craftbukkitVersion = 'UNSPECIFIED'
}*/

if (!project.hasProperty('craftbukkitVersion')) {
	throw new GradleException("Property 'craftbukkitVersion' must be defined before applying this script.")
}

repositories {
	// Paper
	maven { url = 'https://repo.papermc.io/repository/maven-public/' }
}

dependencies {
	implementation project(':shopkeepers-main')
	paperweight.paperDevBundle(craftbukkitVersion) // Includes CraftBukkit and PaperAPI

	testImplementation libs.junit
	testImplementation libs.asm
}

// Note: We use Mojang mappings instead of Spigot mappings on Paper: Mojang mappings are usually
// supported quicker and support for Spigot might be pending removal.
// Dist module: Using the Mojang mappings requires adding a manifest attribute to the final jar.

afterEvaluate {
	configurations {
		create("remapped") {
			canBeConsumed = true
			canBeResolved = false
			attributes {
				attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category, Category.LIBRARY))
				attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, Usage.JAVA_RUNTIME))
			}
		}
	}

	artifacts {
		// Already using Mojang mappings: No further remapping needed.
		remapped tasks.named('jar').map { it.archiveFile }
	}
}

// Note: The compat modules are not published as standalone artifacts, but are shaded into the final plugin jar.
