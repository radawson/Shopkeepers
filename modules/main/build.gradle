plugins {
	id 'java-library'
	id 'org.checkerframework'
	id 'maven-publish'
	id 'com.gradleup.shadow'
	id 'eclipse'
}

repositories {
	// WorldGuard
	maven { url = 'https://maven.enginehub.org/repo/' }
	// Citizens
	maven { url = 'https://maven.citizensnpcs.co/repo' }
	// bStats
	maven { url = 'https://repo.codemc.org/repository/maven-public/' }
	// Towny
	maven { url = 'https://repo.glaremasters.me/repository/towny/' }
	// ServiceIO (replaces Vault)
	maven { url = 'https://repo.thenextlvl.net/releases' }
	// Vault API (for compilation - ServiceIO implements Vault Economy interface)
	maven { url = 'https://nexus.hc.to/content/repositories/pub_releases' }
	// Towny alternative (needs a different group id), legacy VaultAPI (for compatibility)
	maven { url = 'https://jitpack.io' }
}

configurations {
	// Separate configuration for the shaded dependencies to not also include the '*.eea' files.
	shaded
	implementation {
		// Removed from Bukkit in newer versions:
		exclude group: 'commons-lang', module: 'commons-lang'

		// We use CheckerFramework:
		exclude group: 'com.google.code.findbugs', module: 'jsr305'
		// This artifact also includes jsr305, so we exclude it:
		exclude group: 'com.sk89q.worldedit.worldedit-libs', module: 'core'
	}
}

dependencies {
	api project(':shopkeepers-api')
	// compileOnly: Omits these dependencies from the transitively inherited apiElements and
	// runtimeElements. However, we need to manually add these dependencies to our test dependencies
	// if needed.
	compileOnly libs.spigot.api // Includes the Bukkit API
	compileOnly libs.worldguard.bukkit
	compileOnly libs.towny
	(compileOnly libs.citizens.main) { transitive = false }
	// ServiceIO (replaces Vault - implements Vault Economy interface for compatibility)
	compileOnly libs.service.io
	// Vault API for compilation (ServiceIO implements Vault Economy interface)
	compileOnly('com.github.MilkBowl:VaultAPI:1.7') {
		exclude group: 'org.bukkit', module: 'bukkit'
		exclude group: 'com.google.guava', module: 'guava'
	}
	compileOnly libs.bstats.bukkit

	shaded libs.bstats.bukkit

	testImplementation libs.spigot.api
	testImplementation libs.junit
	testImplementation libs.hamcrest
	(testImplementation libs.craftbukkit) {
		artifact {
			classifier = 'remapped-mojang'
		}
	}
}

// Note: There is no need to remap the test classes to run them.
test {
	exclude 'com/nisovin/shopkeepers/PerformanceTests.class'
}

processResources {
	inputs.property 'pluginVersion', pluginVersion
	inputs.property 'dboUrl', dboUrl

	filesMatching('plugin.yml') {
		expand([
			'pluginVersion': pluginVersion,
			'dboUrl': dboUrl
		])
	}
}

jar {
	// We only require the output of the shadowJar task.
	enabled = false
}

/*
java {
	withJavadocJar()
}

javadocJar {
	configureJarTask(project, it)
}

javadoc {
	//options.addStringOption('Xdoclint:none')
	options.addStringOption('Xmaxwarns', '1')
}
*/

shadowJar {
	configureJarTaskWithMavenMetadata(project, it)
	// No classifier: Replaces the normal classifier-less jar file (if there is one).
	archiveClassifier = ''
	configurations = [project.configurations.shaded]
	/*configurations = [project.configurations.compileClasspath]
	dependencies {
		include(dependency(libs.bstats.bukkit.get()))
	}*/
	relocate 'org.bstats.bukkit', "${project.group}.libs.bstats"
}

configureShadowArtifacts(project)

// Copies the project's jars into the build folder of the root project.
task copyResults(type: Copy) {
	from shadowJar
	into rootProject.buildDir
}

assemble {
	dependsOn shadowJar
	dependsOn copyResults
}

publishing {
	publications {
		mavenJava(MavenPublication) { publication ->
			configureShadowMavenPublication(project, publication)
		}
	}
}
